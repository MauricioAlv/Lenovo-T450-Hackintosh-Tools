#Maintained by: dxxs for: 派奇
#battery_Dell_XPS_14Z.txt

# version(版本): 2.0
# Update Time(更新时间) : 2014-6-26

# initial work by dxxs 2014-06-20, 由 dxxs 制作 

# works for: 针对
#  Dell XPS 14Z

# Tip: If you get a 0% battery status,you should also patch the Rehabman's "Fix Mutex with non-zero SyncLevel" patch.
# You also can patch the both of your computer's battery patch and the Rehabman's patch at one time.

# 注意：如果打过电量补丁后，有获取的电池状态显示为0%的情况，还需要打 Rehabman的 “Fix Mutex with non-zero SyncLevel” 补丁。
# 你也可以，一次性打好 自己电脑的电量补丁 和 Rehabman 的那个补丁。

# ==== Field devide 字段拆分 ====
#BRC0
into device label EC0 code_regex BRC0,\s+16 replace_matched begin BRC0,8,BRC1,8 end;
#BSN0
into device label EC0 code_regex BSN0,\s+16 replace_matched begin BSN0,8,BSN1,8 end;
#BPV0
into device label EC0 code_regex BPV0,\s+16 replace_matched begin BPV0,8,BPV1,8 end;
#BDV0
into device label EC0 code_regex BDV0,\s+16 replace_matched begin BDV0,8,BDV1,8 end;
#BDC0
into device label EC0 code_regex BDC0,\s+16 replace_matched begin BDC0,8,BDC1,8 end;
#BFC0
into device label EC0 code_regex BFC0,\s+16 replace_matched begin BFC0,8,BFC1,8 end;
#BAC0
into device label EC0 code_regex BAC0,\s+16 replace_matched begin BAC0,8,BAC1,8 end;
#ERIB
into device label EC0 code_regex ERIB,\s+16, replace_matched begin ERI0,8,ERI1,8, end;

# ==== Replace called method 替换调用方法 ====
#BATN
into method label _BIF code_regex \^\^EC0.BATN replaceall_matched begin ^^EC0.RECB(0x90, 128) end;
#BRC0
into method label _BST code_regex \^\^EC0.BRC0 replaceall_matched begin B1B2(^^EC0.BRC0,^^EC0.BRC1) end;
#BSN0
into method label _BIF code_regex \^\^EC0.BSN0 replaceall_matched begin B1B2(^^EC0.BSN0,^^EC0.BSN1) end;
#BPV0
into method label _BST code_regex \^\^EC0.BPV0 replaceall_matched begin B1B2(^^EC0.BPV0,^^EC0.BPV1) end;
#BDV0
into method label _BIF code_regex \^\^EC0.BDV0 replaceall_matched begin B1B2(^^EC0.BDV0,^^EC0.BDV1) end;
#BDC0
into method label _BIF code_regex \^\^EC0.BDC0 replaceall_matched begin B1B2(^^EC0.BDC0,^^EC0.BDC1) end;
#BFC0
into method label _BIF code_regex \^\^EC0.BFC0 replaceall_matched begin B1B2(^^EC0.BFC0,^^EC0.BFC1) end;
#BAC0
into method label _BST code_regex \^\^EC0.BAC0 replaceall_matched begin B1B2(^^EC0.BAC0,^^EC0.BAC1) end;
#ERIB
into method label FANG code_regex Store\s\(Arg0,\sERIB\) replaceall_matched begin Store(ShiftRight(Arg0,8), ERI1)\nStore(Arg0, ERI0) end;
into method label FANW code_regex Store\s\(Arg0,\sERIB\) replaceall_matched begin Store(ShiftRight(Arg0,8), ERI1)\nStore(Arg0, ERI0) end;

# ==== Data handling method 数据处理方法 ====
# utility methods to read/write buffers from/to EC
into method label RE1B parent_label EC0 remove_entry;
into method label RECB parent_label EC0 remove_entry;
into device label EC0 insert
begin
Method (RE1B, 1, NotSerialized)\n
{\n
    OperationRegion(ERAM, EmbeddedControl, Arg0, 1)\n
    Field(ERAM, ByteAcc, NoLock, Preserve) { BYTE, 8 }\n
    Return(BYTE)\n
}\n
Method (RECB, 2, Serialized)\n
{\n
    ShiftRight(Arg1, 3, Arg1)\n
    Name(TEMP, Buffer(Arg1) { })\n
    Add(Arg0, Arg1, Arg1)\n
    Store(0, Local0)\n
    While (LLess(Arg0, Arg1))\n
    {\n
        Store(RE1B(Arg0), Index(TEMP, Local0))\n
        Increment(Arg0)\n
        Increment(Local0)\n
    }\n
    Return(TEMP)\n
}\n
end;

into method label B1B2 remove_entry;
into definitionblock code_regex . insert
begin
Method (B1B2, 2, NotSerialized)\n
{\n
Return(Or(Arg0, ShiftLeft(Arg1, 8)))\n
\n
}\n
end;