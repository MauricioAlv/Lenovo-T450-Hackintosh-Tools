#Maintained by: dxxs for: 派奇
#battery_HP_Envy6_1005TX.txt

# version(版本): 2.0
# Update Time(更新时间) : 2014-6-26

# initial work by dxxs 2014-06-20, 由 dxxs 制作 

# works for: 针对
#  HP Envy6 1005TX

# Tip: If you get a 0% battery status,you should also patch the Rehabman's "Fix Mutex with non-zero SyncLevel" patch.
# You also can patch the both of your computer's battery patch and the Rehabman's patch at one time.

# 注意：如果打过电量补丁后，有获取的电池状态显示为0%的情况，还需要打 Rehabman的 “Fix Mutex with non-zero SyncLevel” 补丁。
# 你也可以，一次性打好 自己电脑的电量补丁 和 Rehabman 的那个补丁。

# ==== Field devide 字段拆分 ====
#MUAC
into device label EC0 code_regex MUAC,\s+16, replace_matched begin MUAC,8,MUAD,8, end;
#ATTE
into device label EC0 code_regex ATTE,\s+16, replace_matched begin ATT0,8,ATT1,8, end;
#RTTE
into device label EC0 code_regex RTTE,\s+16, replace_matched begin RTT0,8,RTT1,8, end;
#ATTF
into device label EC0 code_regex ATTF,\s+16, replace_matched begin ATT3,8,ATT4,8, end;
#ERIB
into device label EC0 code_regex ERIB,\s+16, replace_matched begin ERI0,8,ERI1,8, end;

# ==== Replace called method 替换调用方法 ====
#SMD0
into method label CFUN code_regex Store\s\(Zero,\sSMD0\) replaceall_matched begin WECB(0x64,256,Zero) end;
into method label CFUN code_regex \(SMD0 replaceall_matched begin (RECB(0x64,256) end;
into method label ESMR code_regex Store\s\(Zero,\s\^\^LPCB.EC0.SMD0\) replaceall_matched begin ^^LPCB.EC0.WECB(0x64,256,Zero) end;
into method label ESMR code_regex \(\^\^LPCB.EC0.SMD0 replaceall_matched begin (^^LPCB.EC0.RECB(0x64,256) end;
into method label ESMW code_regex Store\s\(LBUF,\s\^\^LPCB.EC0.SMD0\) replaceall_matched begin ^^LPCB.EC0.WECB(0x64,256,LBUF) end;
#BCTL
into method label BATT code_regex \^\^PCI0.LPCB.EC0.BCTL replaceall_matched begin ^^PCI0.LPCB.EC0.RECB(0x2D,128) end;
#BTDC
into method label GUBT code_regex \(BTDC replaceall_matched begin (RECB(0x88,64) end;
into method label BATT code_regex \^\^PCI0.LPCB.EC0.BTDC replaceall_matched begin ^^PCI0.LPCB.EC0.RECB(0x88,64) end;
#BMNN
into method label GUBI code_regex \(BMNN replaceall_matched begin (RECB(0x90,72) end;
into method label BATT code_regex \^\^PCI0.LPCB.EC0.BMNN replaceall_matched begin ^^PCI0.LPCB.EC0.RECB(0x90,72) end;
#BDVN
into method label GBMN code_regex \(BDVN replaceall_matched begin (RECB(0x99,56) end;
into method label BATT code_regex \^\^PCI0.LPCB.EC0.BDVN replaceall_matched begin ^^PCI0.LPCB.EC0.RECB(0x99,56) end;
#MUAC
into method label BATT code_regex \^\^PCI0.LPCB.EC0.MUAC replaceall_matched begin B1B2(^^PCI0.LPCB.EC0.MUAC,^^PCI0.LPCB.EC0.MUAD) end;
#ATTE
into method label BATT code_regex \^\^PCI0.LPCB.EC0.ATTE replaceall_matched begin B1B2(^^PCI0.LPCB.EC0.ATT0,^^PCI0.LPCB.EC0.ATT1) end;
#RTTE
into method label BATT code_regex \^\^PCI0.LPCB.EC0.RTTE replaceall_matched begin B1B2(^^PCI0.LPCB.EC0.RTT0,^^PCI0.LPCB.EC0.RTT1) end;
#ATTF
into method label BATT code_regex \^\^PCI0.LPCB.EC0.ATTF replaceall_matched begin B1B2(^^PCI0.LPCB.EC0.ATT3,^^PCI0.LPCB.EC0.ATT4) end;
#ERIB
into method label FANG code_regex Store\s\(Arg0,\sERIB\) replaceall_matched begin Store(ShiftRight(Arg0,8), ERI1)\nStore(Arg0, ERI0) end;
into method label FANW code_regex Store\s\(Arg0,\sERIB\) replaceall_matched begin Store(ShiftRight(Arg0,8), ERI1)\nStore(Arg0, ERI0) end;

# ==== Data handling method 数据处理方法 ====
into method label RE1B parent_label EC0 remove_entry;
into method label RECB parent_label EC0 remove_entry;
into device label EC0 insert
begin
Method (RE1B, 1, NotSerialized)\n
{\n
    OperationRegion(ERAM, EmbeddedControl, Arg0, 1)\n
    Field(ERAM, ByteAcc, NoLock, Preserve) { BYTE, 8 }\n
    Return(BYTE)\n
}\n
Method (RECB, 2, Serialized)\n
{\n
    ShiftRight(Arg1, 3, Arg1)\n
    Name(TEMP, Buffer(Arg1) { })\n
    Add(Arg0, Arg1, Arg1)\n
    Store(0, Local0)\n
    While (LLess(Arg0, Arg1))\n
    {\n
        Store(RE1B(Arg0), Index(TEMP, Local0))\n
        Increment(Arg0)\n
        Increment(Local0)\n
    }\n
    Return(TEMP)\n
}\n
end;

into method label WE1B parent_label EC0 remove_entry;
into method label WECB parent_label EC0 remove_entry;
into device label EC0 insert
begin
Method (WE1B, 2, NotSerialized)\n
{\n
    OperationRegion(ERAM, EmbeddedControl, Arg0, 1)\n
    Field(ERAM, ByteAcc, NoLock, Preserve) { BYTE, 8 }\n
    Store(Arg1, BYTE)\n
}\n
Method (WECB, 3, Serialized)\n
// Arg0 - offset in bytes from zero-based EC\n
// Arg1 - size of buffer in bits\n
// Arg2 - value to write\n
{\n
    ShiftRight(Arg1, 3, Arg1)\n
    Name(TEMP, Buffer(Arg1) { })\n
    Store(Arg2, TEMP)\n
    Add(Arg0, Arg1, Arg1)\n
    Store(0, Local0)\n
    While (LLess(Arg0, Arg1))\n
    {\n
        WE1B(Arg0, DerefOf(Index(TEMP, Local0)))\n
        Increment(Arg0)\n
        Increment(Local0)\n
    }\n
}\n
end;

into method label B1B2 remove_entry;
into definitionblock code_regex . insert
begin
Method (B1B2, 2, NotSerialized)\n
{\n
Return(Or(Arg0, ShiftLeft(Arg1, 8)))\n
\n
}\n
end;